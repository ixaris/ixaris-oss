apply from: "$rootDir/gradle/repositories.gradle"
apply from: "$rootDir/gradle/versions.gradle"

apply plugin: 'java'
apply plugin: 'maven-publish'

dependencies {
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    if (project.name != 'ix-commons-misc-test') {
        testCompile project(':ix-commons-misc-test')
    }
}

sourceCompatibility = '11'
targetCompatibility = '11'

// Force main rebuild if resources are changed
if (file('src/main/resources').exists()) {
    compileJava.inputs.dir('src/main/resources')
}

// Force test rebuild if resources are changed
if (file('src/test/resources').exists()) {
    compileTestJava.inputs.dir('src/test/resources')
}

// junit 5
test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Version': version
        // so we can call this.getClass().getPackage().getImplementationVersion()
    }
}

tasks.create('sourcesJar', Jar) {
    dependsOn = [classes]
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}
